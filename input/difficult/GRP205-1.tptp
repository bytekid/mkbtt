%--------------------------------------------------------------------------
% File     : GRP205-1 : TPTP v3.6.0. Released v2.3.0.
% Domain   : Group Theory (Loops)
% Problem  : In Loops, Moufang-3 => Moufang-4.
% Version  : [MP96] (equality) axioms.
% English  : 

% Refs     : [Wos96] Wos (1996), OTTER and the Moufang Identity Problem
% Source   : [Wos96]
% Names    : - [Wos96]

% Status   : Unsatisfiable
% Rating   : 0.33 v3.4.0, 0.38 v3.3.0, 0.43 v3.1.0, 0.33 v2.7.0, 0.36 v2.6.0, 0.00 v2.4.0, 0.33 v2.3.0
% Syntax   : Number of clauses     :   10 (   0 non-Horn;  10 unit;   1 RR)
%            Number of atoms       :   10 (  10 equality)
%            Maximal clause size   :    1 (   1 average)
%            Number of predicates  :    1 (   0 propositional; 2-2 arity)
%            Number of functors    :    9 (   4 constant; 0-2 arity)
%            Number of variables   :   15 (   0 singleton)
%            Maximal term depth    :    4 (   2 average)

% Comments :
%--------------------------------------------------------------------------
%----Loop axioms:
cnf(left_identity,axiom,
    ( multiply(identity,X) = X )).

cnf(right_identity,axiom,
    ( multiply(X,identity) = X )).

cnf(multiply_left_division,axiom,
    ( multiply(X,left_division(X,Y)) = Y )).

cnf(left_division_multiply,axiom,
    ( left_division(X,multiply(X,Y)) = Y )).

cnf(multiply_right_division,axiom,
    ( multiply(right_division(X,Y),Y) = X )).

cnf(right_division_multiply,axiom,
    ( right_division(multiply(X,Y),Y) = X )).

cnf(right_inverse,axiom,
    ( multiply(X,right_inverse(X)) = identity )).

cnf(left_inverse,axiom,
    ( multiply(left_inverse(X),X) = identity )).

%----Moufang-3
cnf(moufang3,axiom,
    ( multiply(multiply(multiply(X,Y),X),Z) = multiply(X,multiply(Y,multiply(X,Z))) )).

%----Denial of Moufang-4
cnf(prove_moufang4,negated_conjecture,
    (  multiply(x,multiply(multiply(y,z),x)) != multiply(multiply(x,y),multiply(z,x)) )).

%--------------------------------------------------------------------------
