%--------------------------------------------------------------------------
% File     : GRP201-1 : TPTP v3.6.0. Released v2.2.0.
% Domain   : Group Theory (Loops)
% Problem  : In Loops, Moufang-2 => Moufang-3.
% Version  : [MP96] (equality) axioms.
% English  : 

% Refs     : [McC98] McCune (1998), Email to G. Sutcliffe
%          : [Wos96] Wos (1996), OTTER and the Moufang Identity Problem
%          : [MP96]  McCune & Padmanabhan (1996), Automated Deduction in Eq
% Source   : [McC98]
% Names    : MFL-2 [MP96]
%          : - [Wos96]

% Status   : Unsatisfiable
% Rating   : 0.22 v3.4.0, 0.25 v3.3.0, 0.36 v3.1.0, 0.44 v2.7.0, 0.36 v2.6.0, 0.00 v2.2.1
% Syntax   : Number of clauses     :   10 (   0 non-Horn;  10 unit;   1 RR)
%            Number of atoms       :   10 (  10 equality)
%            Maximal clause size   :    1 (   1 average)
%            Number of predicates  :    1 (   0 propositional; 2-2 arity)
%            Number of functors    :    9 (   4 constant; 0-2 arity)
%            Number of variables   :   15 (   0 singleton)
%            Maximal term depth    :    4 (   2 average)

% Comments :
%--------------------------------------------------------------------------
%----Loop axioms:
cnf(left_identity,axiom,
    ( multiply(identity,X) = X )).

cnf(right_identity,axiom,
    ( multiply(X,identity) = X )).

cnf(multiply_left_division,axiom,
    ( multiply(X,left_division(X,Y)) = Y )).

cnf(left_division_multiply,axiom,
    ( left_division(X,multiply(X,Y)) = Y )).

cnf(multiply_right_division,axiom,
    ( multiply(right_division(X,Y),Y) = X )).

cnf(right_division_multiply,axiom,
    ( right_division(multiply(X,Y),Y) = X )).

cnf(right_inverse,axiom,
    ( multiply(X,right_inverse(X)) = identity )).

cnf(left_inverse,axiom,
    ( multiply(left_inverse(X),X) = identity )).

%----Moufang-2:
cnf(moufang2,axiom,
    ( multiply(multiply(multiply(X,Y),Z),Y) = multiply(X,multiply(Y,multiply(Z,Y))) )).

%----Denial of Moufang-3:
cnf(prove_moufang3,negated_conjecture,
    (  multiply(multiply(multiply(a,b),a),c) != multiply(a,multiply(b,multiply(a,c))) )).

%--------------------------------------------------------------------------
